plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	/*used to integrate Protocol Buffers support into gradle project*/
	id 'com.google.protobuf' version '0.9.4'	
}
protobuf {
	/*used to set the version of the Protobuf compiler*/
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.1"
	}
	/*code generator(java code) from .proto files that works alongside the protocol buffers compiler*/
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.63.0'
		}
	}
	/*ensures that gRPC plugin is applied to all protobuf generation tasks*/
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}
group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	//grpc dependencies
	/*necessary runtime support for gRPC communication over the Netty transport.*/
	runtimeOnly 'io.grpc:grpc-netty-shaded:1.63.0'
	/*support for serializing and deserializing protocol buffer messages used in gRPC communication.*/
	implementation 'io.grpc:grpc-protobuf:1.63.0'
	implementation 'io.grpc:grpc-stub:1.63.0'
	implementation 'com.atomikos:transactions-jta:5.0.6'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.7'
}

tasks.named('test') {
	useJUnitPlatform()
}
